import org.apache.tools.ant.DirectoryScanner


plugins {
    id 'java-library'
}

group 'edu.depaul'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    def junitVersion = '5.7.1'
    def logbackVersion = '1.2.4'

    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation "ch.qos.logback:logback-classic:$logbackVersion"
    implementation "ch.qos.logback:logback-core:$logbackVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

//TODO: change to your userid
def user = 'ZKHAN41'

task packageDistribution(type: Zip) {
    doFirst{
        DirectoryScanner.removeDefaultExclude("**/.gitignore")
    }
    dependsOn check
    archiveFileName = "${user}-${rootProject.name}.zip"
    destinationDirectory = file("$buildDir/dist")

    from("$projectDir") {
        include "src/**/*"
        include "build.gradle"
        include "gradlew"
        include "gradlew.bat"
        include "settings.gradle"
        include ".gitignore"
    }

    into("${rootProject.name}")
}

test {
    useJUnitPlatform()
    afterTest { desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

check {
    finalizedBy packageDistribution
}

